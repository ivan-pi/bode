c     --------------------------------------------
c     Subroutine solve -- Partial pivoting version
c
c     Solves the system Ax = b given a  right-hand
c     side b.  Solution x is overwritten onto vec-
c     tor b.  a(n,m) contains the LU factored form 
c     of A generated by subroutine band. p(n) con-
c     tains the pivoting information  returned  by 
c     band.
c     --------------------------------------------
      subroutine solve(a,b,m,n,p)
      integer i,j,k,m,n,p(n),r
      complex a(n,1),b(n),c

      r = (m+1)/2
c
c     Forward elimination
c
      do 100 k = 1,n-1
      c = b(k)
      b(k) = b(p(k))
      b(p(k)) = c

        i = k+1
        j = r-1
  110   if(j.lt.1 .or. i.gt.n) goto 100
        b(i) = b(i) - a(i,j)*b(k)
        i = i + 1
        j = j - 1
        goto 110
  100 continue
c
c     Back substitution
c
      do 120 k = n,1,-1
        i = k+1
        j = r+1
  130   if(j.gt.m+r-1 .or. i.gt.n) goto 140
        b(k) = b(k) - a(k,j)*b(i)
        i = i + 1
        j = j + 1
        goto 130
  140 continue
      b(k) = b(k) * a(k,r)
  120 continue
      return
      end