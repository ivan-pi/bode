#:for c, lit in zip(['s','d'],["1.0e0","1.0d0"])
c     ---------------------------------------------------
c     Subroutine ${c}$bands -- Partial pivoting version
c
c     Solves the system Ax = b given a right-hand side b.
C     Solution x is overwritten onto vector b. a(n,m)
C     contains the LU factored form of A generated by
c     subroutine band. p(n) contains the pivoting
c     information returned by band.
c     ---------------------------------------------------
      subroutine ${c}$bands(n,m,a,lda,p,b,info)
      implicit none
      integer, parameter :: rk = kind(${lit}$)
      integer, intent(in) :: n,m,lda
      real(kind=rk), intent(in) :: a(lda,*)
      integer, intent(in) :: p(n)
      real(kind=rk), intent(inout) :: b(n)
      integer, intent(out) :: info

      integer :: i, j, k, r
      real(kind=rk) :: c

      info = 0
      if (n .lt. 0) then
        info = -1
      else if (m .lt. 0) then
        info = -2
      else if (lda .lt. n) then
        info = -4
      end if

      if (info .ne. 0) return

c     Quick return if possible
      if (n .eq. 0) return
      if (m .eq. 0) return

      r = (m+1)/2
c
c     Forward elimination
c
      do 100 k = 1,n-1
         c = b(k)
         b(k) = b(p(k))
         b(p(k)) = c
         i = k+1
         j = r-1
  110    if (j.lt.1 .or. i.gt.n) goto 100
         b(i) = b(i) - a(i,j)*b(k)
         i = i + 1
         j = j - 1
         goto 110
  100 continue
c
c     Back substitution
c
      do 120 k = n,1,-1
         i = k+1
         j = r+1
  130    if (j.gt.m+r-1 .or. i.gt.n) goto 140
         b(k) = b(k) - a(k,j)*b(i)
         i = i+1
         j = j+1
         goto 130
  140    continue
         b(k) = b(k) * a(k,r)
  120 continue
      return
      end
c
#:endfor
